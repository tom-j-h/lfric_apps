!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Routine for transporting the moisture mixing ratio fields.

module moist_mr_transport_alg_mod

  use constants_mod,                  only: i_def, r_def
  use fem_constants_mod,              only: get_inverse_lumped_mass_matrix, &
                                            get_mass_matrix_diagonal
  use field_mod,                      only: field_type
  use io_config_mod,                  only: subroutine_timers
  use log_mod,                        only: log_event,       &
                                            LOG_LEVEL_ERROR
  use mesh_mod,                       only: mesh_type
  use mesh_collection_mod,            only: mesh_collection
  use mr_indices_mod,                 only: nummr
  use timer_mod,                      only: timer
  use transport_enumerated_types_mod, only: equation_form_advective,    &
                                            equation_form_conservative, &
                                            equation_form_consistent
  use transport_field_mod,            only: transport_field
  use transport_metadata_mod,         only: transport_metadata_type
  use transport_rho_times_field_alg_mod,  &
                                      only: transport_rho_times_field_alg
  use transport_runtime_alg_mod,      only: transport_runtime_type
  use transport_runtime_collection_mod,   &
                                      only: get_transport_runtime

  implicit none

  private

  public :: moist_mr_transport_alg

contains

  !> @brief Central routine for transporting moisture mixing ratio fields.
  !> @details Performs a whole transport time step for moisture mixing ratios,
  !!          with different routines called depending on the form of the
  !!          transport equation being used. If transport equation is
  !!          conservative, then transforms mixing ratios to densities on the
  !!          shifted mesh before transporting these.
  !> @param[in,out] mr_out             Moisture mixing ratios after transport
  !> @param[in]     mr_in              Moisture mixing ratios before transport
  !> @param[in]     nummr_to_transport Number of moisture species to transport
  !> @param[in]     model_dt           Model timestep
  !> @param[in]     transport_metadata Contains the configuration options for
  !!                                   transporting these fields
  subroutine moist_mr_transport_alg(mr_out_rdef, mr_in_rdef, nummr_to_transport, &
                                    model_dt_rdef, transport_metadata)

    implicit none

    ! Arguments
    type(field_type),              intent(inout) :: mr_out_rdef(nummr)
    type(field_type),              intent(in)    :: mr_in_rdef(nummr)
    integer(kind=i_def),           intent(in)    :: nummr_to_transport
    real(kind=r_def),              intent(in)    :: model_dt_rdef
    type(transport_metadata_type), intent(in)    :: transport_metadata

    ! Internal variables
    integer(kind=i_def)                :: imr

    ! Transport runtime and form of transport equation
    type(transport_runtime_type), pointer :: transport_runtime
    integer(kind=i_def)                   :: equation_form

    if ( subroutine_timers ) call timer('transport.moisture')

    do imr = 1, nummr_to_transport

      ! Get correct equation form depending on advective form flag
      transport_runtime => get_transport_runtime(mr_in_rdef(imr)%get_mesh())
      if ( transport_runtime%get_advective_flag() ) then
        equation_form = equation_form_advective
      else
        equation_form = transport_metadata%get_equation_form()
      end if
      nullify( transport_runtime )

      ! ------------------------------------------------------------------------ !
      ! Transport depending on equation
      ! If the equation form is advective:
      !           Simply transport field in native space
      ! If the equation form is consistent:
      !           Requires transformation to densities and evaluation of fluxes.
      !           This is done in the lowest level algorithms, so can just call
      !           transport_field.
      ! If the equation form is conservative:
      !           This also requires transformation to densities and evaluation
      !           of fluxes, but is achieved using the transport_rho_times_field
      !           algorithm (this also divides the result by density at the end
      !           of the transport step and so returns an updated field.)
      ! ------------------------------------------------------------------------ !
      select case ( equation_form )

        ! ---------------------------------------------------------------------- !
        ! Advective and consistent forms of transport equation
        ! ---------------------------------------------------------------------- !
      case ( equation_form_advective, equation_form_consistent )

        call transport_field(mr_out_rdef(imr), mr_in_rdef(imr), model_dt_rdef, &
                             transport_metadata)

        ! ---------------------------------------------------------------------- !
        ! Conservative form of transport equation
        ! ---------------------------------------------------------------------- !
      case ( equation_form_conservative )

        call transport_rho_times_field_alg(mr_out_rdef(imr), mr_in_rdef(imr),  &
                                           model_dt_rdef, transport_metadata)

        ! ---------------------------------------------------------------------- !
        ! Default form of transport equation
        ! ---------------------------------------------------------------------- !
      case default

        call log_event('Form of moisture transport equation either not ' //    &
                       'compatible or not implemented', LOG_LEVEL_ERROR)

      end select

    end do

    ! -------------------------------------------------------------------------- !
    ! Copy non-transported fields over
    ! -------------------------------------------------------------------------- !

    do imr = nummr_to_transport + 1, nummr
      call mr_in_rdef(imr)%copy_field_properties(mr_out_rdef(imr))
      call invoke( setval_X(mr_out_rdef(imr), mr_in_rdef(imr)) )
    end do

    if ( subroutine_timers ) call timer('transport.moisture')

  end subroutine moist_mr_transport_alg

end module moist_mr_transport_alg_mod
